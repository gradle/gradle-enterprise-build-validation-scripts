import com.gradle.enterprise.gradleplugin.GradleEnterprisePlugin
import com.gradle.scan.plugin.BuildScanPlugin
import com.gradle.CommonCustomUserDataGradlePlugin
import org.gradle.util.GradleVersion

initscript {
    def gradleEnterprisePluginVersion = "3.11.1"
    def commonCustomUserDataPluginVersion = "1.8"

    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath("com.gradle:gradle-enterprise-gradle-plugin:${gradleEnterprisePluginVersion}")
        classpath("com.gradle:common-custom-user-data-gradle-plugin:${commonCustomUserDataPluginVersion}")
    }
}

// Don't run against the included builds (if the main build has any).
def isTopLevelBuild = gradle.getParent() == null
if (isTopLevelBuild) {
    def version = GradleVersion.current().baseVersion
    def atLeastGradle5 = version >= GradleVersion.version("5.0")
    def atLeastGradle6 = version >= GradleVersion.version("6.0")

    if (atLeastGradle6) {
        beforeSettings { settings ->
            settings.pluginManager.apply(GradleEnterprisePlugin)
            settings.pluginManager.apply(CommonCustomUserDataGradlePlugin)
        }
    } else if (atLeastGradle5) {
        rootProject {
            pluginManager.apply(BuildScanPlugin)
            pluginManager.apply(CommonCustomUserDataGradlePlugin)
        }
    } else {
        throw new IllegalStateException("Build validation not supported for Gradle ${GradleVersion.current()}. Upgrade your project's build to Gradle 5 or newer.")
    }
}

